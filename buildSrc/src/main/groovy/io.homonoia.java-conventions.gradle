import java.time.LocalDate

plugins {
    id 'java-library'
    id 'com.github.joschi.licenser'
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group = 'org.homonoia'
version = rootProject.version

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

license {
    header = rootProject.file('license_header.txt')
    include '**/*.java' include '**/*.java'
    ext {
        year = LocalDate.now().getYear()
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/alexparlett/simple-rules"
            credentials {
                username = System.getProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
                password = System.getProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
